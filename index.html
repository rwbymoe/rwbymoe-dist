<!DOCTYPE html><html><head><meta charset="utf-8"><meta name="viewport" content="width=device-width,initial-scale=1,maximum-scale=1,user-scalable=no"><meta http-equiv="X-UA-Compatible" content="IE=edge"><title>Shattered Moon</title><link rel="stylesheet" type="text/css" href="style.5025b8fe.css"><link rel="shortcut icon" type="image/x-icon" href="nc-fav.c08a0d16.ico"><link rel="stylesheet" type="text/css" href="main.7bbe976e.css"></head><body class="single-image" onselectstart="return!1"><div class="page-loader-wrapper"><div style="position:absolute;left:0;right:0;bottom:30%;text-align:center;min-width:18rem;text-shadow:0 0 .5rem">从Github载入中，等不及请访问 <a href="https://rwbymoe.gitee.io">Gitee镜像</a></div><div class="loader"><link rel="preload" href="partical-bg.98672c97.png" as="image"></div></div><div id="main-wrapper"><main id="main" class="background" style="background-image:url(partical-bg.98672c97.png)"><div class="overlay" style="background-color:rgba(0,0,0,.3)"></div><div class="nc-content-section index nc-active vhm"><canvas id="sakura"></canvas><div id="home-page" class="page-wrapper vhm-item active-home anim s01"><div class="container"><div class="logo ac anim fadeInUp s01 delay-0-5s"><div class="logo-wrp"><img src="logo.27ea9ee2.png" alt="Logo"></div></div><div class="tagline ac anim fadeInUp s01 delay-0-6s"><div class="carousel-widget text-carousel" data-itemrange="false" data-in="fadeIn" data-out="fadeOut" data-autoplay="true" data-loop="true" data-items="1" data-mousedrag="false" data-touchdrag="false"><div class="carousel"><div class="owl-carousel owl-theme"><div class="item"><h1>Hello</h1><p>Welcome to My Personal Pages!</p></div><div class="item"><h1>你好</h1><p>欢迎来到我的个人主页！</p></div><div class="item"><h1>Bonjour</h1><p>C'est ma page d'accueil.</p></div><div class="item"><h1>Hallo</h1><p>Das ist meine Persönliche homepage.</p></div><div class="item"><h1>こんにちは</h1><p>これは私の個人のホームページです.</p></div><div class="item"><h1>Ciao</h1><p>Questa è la mia Pagina personale.</p></div><div class="item"><h1>Привет</h1><p>Это моя  личная страница .</p></div></div></div></div></div><div class="social-icon ac anim fadeInUp s01 delay-0-9s"><a class="icon vhm" target="_blank" href="https://rwbymoe.gitee.io/blog"><i class="fa icon-CN_codingnet vhm-item"></i> </a><a class="icon vhm" target="_blank" href="https://space.bilibili.com/5664286"><i class="fa icon-CN_bilibili vhm-item"></i> </a><a class="icon vhm" target="_blank" href="https://www.zhihu.com/people/heureka"><i class="fa icon-CN_zhihu vhm-item"></i> </a><a class="icon vhm" target="_blank" href="https://twitter.com/kao789789789"><i class="fa icon-Popular_Twitter vhm-item"></i></a></div><div class="copyrights ac anim fadeInUp s01 delay-1s">RWBY.MOE<br><br><span id="aa"></span><script language="javascript">function show_date_time(){setInterval(function(){timeold=(new Date).getTime()-new Date("06/07/2018 00:00:00").getTime(),sectimeold=timeold/1e3,secondsold=Math.floor(sectimeold),msPerDay=864e5,e_daysold=timeold/msPerDay,daysold=Math.floor(e_daysold),e_hrsold=24*(e_daysold-daysold),hrsold=Math.floor(e_hrsold),e_minsold=60*(e_hrsold-hrsold),minsold=Math.floor(60*(e_hrsold-hrsold)),seconds=Math.floor(60*(e_minsold-minsold)),aa.innerHTML=daysold+"D "+hrsold+"H "+minsold+"M "+seconds+"S "},1e3)}show_date_time()</script><br><br><br><br>2018&nbsp;-&nbsp;<script>document.write((new Date).getFullYear())</script>&nbsp;Heureka</div></div></div></div><div class="nc-overlay"></div><script id="sakura_point_vsh" type="x-shader/x_vertex">uniform mat4 uProjection;
uniform mat4 uModelview;
uniform vec3 uResolution;
uniform vec3 uOffset;
uniform vec3 uDOF;  //x:focus distance, y:focus radius, z:max radius
uniform vec3 uFade; //x:start distance, y:half distance, z:near fade start

attribute vec3 aPosition;
attribute vec3 aEuler;
attribute vec2 aMisc; //x:size, y:fade

varying vec3 pposition;
varying float psize;
varying float palpha;
varying float pdist;

//varying mat3 rotMat;
varying vec3 normX;
varying vec3 normY;
varying vec3 normZ;
varying vec3 normal;

varying float diffuse;
varying float specular;
varying float rstop;
varying float distancefade;

void main(void) {
	// Projection is based on vertical angle
	vec4 pos = uModelview * vec4(aPosition + uOffset, 1.0);
	gl_Position = uProjection * pos;
	gl_PointSize = aMisc.x * uProjection[1][1] / -pos.z * uResolution.y * 0.5;

	pposition = pos.xyz;
	psize = aMisc.x;
	pdist = length(pos.xyz);
	palpha = smoothstep(0.0, 1.0, (pdist - 0.1) / uFade.z);

	vec3 elrsn = sin(aEuler);
	vec3 elrcs = cos(aEuler);
	mat3 rotx = mat3(
			1.0, 0.0, 0.0,
			0.0, elrcs.x, elrsn.x,
			0.0, -elrsn.x, elrcs.x
	);
	mat3 roty = mat3(
			elrcs.y, 0.0, -elrsn.y,
			0.0, 1.0, 0.0,
			elrsn.y, 0.0, elrcs.y
	);
	mat3 rotz = mat3(
			elrcs.z, elrsn.z, 0.0,
			-elrsn.z, elrcs.z, 0.0,
			0.0, 0.0, 1.0
	);
	mat3 rotmat = rotx * roty * rotz;
	normal = rotmat[2];

	mat3 trrotm = mat3(
			rotmat[0][0], rotmat[1][0], rotmat[2][0],
			rotmat[0][1], rotmat[1][1], rotmat[2][1],
			rotmat[0][2], rotmat[1][2], rotmat[2][2]
	);
	normX = trrotm[0];
	normY = trrotm[1];
	normZ = trrotm[2];

	const vec3 lit = vec3(0.6917144638660746, 0.6917144638660746, -0.20751433915982237);

	float tmpdfs = dot(lit, normal);
	if(tmpdfs < 0.0) {
			normal = -normal;
			tmpdfs = dot(lit, normal);
	}
	diffuse = 0.4 + tmpdfs;

	vec3 eyev = normalize(-pos.xyz);
	if(dot(eyev, normal) > 0.0) {
			vec3 hv = normalize(eyev + lit);
			specular = pow(max(dot(hv, normal), 0.0), 20.0);
	}
	else {
			specular = 0.0;
	}

	rstop = clamp((abs(pdist - uDOF.x) - uDOF.y) / uDOF.z, 0.0, 1.0);
	rstop = pow(rstop, 0.5);
	//-0.69315 = ln(0.5)
	distancefade = min(1.0, exp((uFade.x - pdist) * 0.69315 / uFade.y));
}</script><script id="sakura_point_fsh" type="x-shader/x_fragment">#ifdef GL_ES
//precision mediump float;
precision highp float;
#endif

uniform vec3 uDOF;  //x:focus distance, y:focus radius, z:max radius
uniform vec3 uFade; //x:start distance, y:half distance, z:near fade start

const vec3 fadeCol = vec3(0.08, 0.03, 0.06);

varying vec3 pposition;
varying float psize;
varying float palpha;
varying float pdist;

//varying mat3 rotMat;
varying vec3 normX;
varying vec3 normY;
varying vec3 normZ;
varying vec3 normal;

varying float diffuse;
varying float specular;
varying float rstop;
varying float distancefade;

float ellipse(vec2 p, vec2 o, vec2 r) {
	vec2 lp = (p - o) / r;
	return length(lp) - 1.0;
}

void main(void) {
	vec3 p = vec3(gl_PointCoord - vec2(0.5, 0.5), 0.0) * 2.0;
	vec3 d = vec3(0.0, 0.0, -1.0);
	float nd = normZ.z; //dot(-normZ, d);
	if(abs(nd) < 0.0001) discard;

	float np = dot(normZ, p);
	vec3 tp = p + d * np / nd;
	vec2 coord = vec2(dot(normX, tp), dot(normY, tp));

	//angle = 15 degree
	const float flwrsn = 0.258819045102521;
	const float flwrcs = 0.965925826289068;
	mat2 flwrm = mat2(flwrcs, -flwrsn, flwrsn, flwrcs);
	vec2 flwrp = vec2(abs(coord.x), coord.y) * flwrm;

	float r;
	if(flwrp.x < 0.0) {
			r = ellipse(flwrp, vec2(0.065, 0.024) * 0.5, vec2(0.36, 0.96) * 0.5);
	}
	else {
			r = ellipse(flwrp, vec2(0.065, 0.024) * 0.5, vec2(0.58, 0.96) * 0.5);
	}

	if(r > rstop) discard;

	vec3 col = mix(vec3(1.0, 0.8, 0.75), vec3(1.0, 0.9, 0.87), r);
	float grady = mix(0.0, 1.0, pow(coord.y * 0.5 + 0.5, 0.35));
	col *= vec3(1.0, grady, grady);
	col *= mix(0.8, 1.0, pow(abs(coord.x), 0.3));
	col = col * diffuse + specular;

	col = mix(fadeCol, col, distancefade);

	float alpha = (rstop > 0.001)? (0.5 - r / (rstop * 2.0)) : 1.0;
	alpha = smoothstep(0.0, 1.0, alpha) * palpha;

	gl_FragColor = vec4(col * 0.5, alpha);
}</script><script id="fx_common_vsh" type="x-shader/x_vertex">uniform vec3 uResolution;
attribute vec2 aPosition;

varying vec2 texCoord;
varying vec2 screenCoord;

void main(void) {
	gl_Position = vec4(aPosition, 0.0, 1.0);
	texCoord = aPosition.xy * 0.5 + vec2(0.5, 0.5);
	screenCoord = aPosition.xy * vec2(uResolution.z, 1.0);
}</script><script id="fx_brightbuf_fsh" type="x-shader/x_fragment">#ifdef GL_ES
//precision mediump float;
precision highp float;
#endif
uniform sampler2D uSrc;
uniform vec2 uDelta;

varying vec2 texCoord;
varying vec2 screenCoord;

void main(void) {
	vec4 col = texture2D(uSrc, texCoord);
	gl_FragColor = vec4(col.rgb * 2.0 - vec3(0.5), 1.0);
}</script><script id="fx_dirblur_r4_fsh" type="x-shader/x_fragment">#ifdef GL_ES
//precision mediump float;
precision highp float;
#endif
uniform sampler2D uSrc;
uniform vec2 uDelta;
uniform vec4 uBlurDir; //dir(x, y), stride(z, w)

varying vec2 texCoord;
varying vec2 screenCoord;

void main(void) {
	vec4 col = texture2D(uSrc, texCoord);
	col = col + texture2D(uSrc, texCoord + uBlurDir.xy * uDelta);
	col = col + texture2D(uSrc, texCoord - uBlurDir.xy * uDelta);
	col = col + texture2D(uSrc, texCoord + (uBlurDir.xy + uBlurDir.zw) * uDelta);
	col = col + texture2D(uSrc, texCoord - (uBlurDir.xy + uBlurDir.zw) * uDelta);
	gl_FragColor = col / 5.0;
}</script><script id="fx_common_fsh" type="x-shader/x_fragment">#ifdef GL_ES
//precision mediump float;
precision highp float;
#endif
uniform sampler2D uSrc;
uniform vec2 uDelta;

varying vec2 texCoord;
varying vec2 screenCoord;

void main(void) {
	gl_FragColor = texture2D(uSrc, texCoord);
}</script><script id="pp_final_vsh" type="x-shader/x_vertex">uniform vec3 uResolution;
attribute vec2 aPosition;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
	gl_Position = vec4(aPosition, 0.0, 1.0);
	texCoord = aPosition.xy * 0.5 + vec2(0.5, 0.5);
	screenCoord = aPosition.xy * vec2(uResolution.z, 1.0);
}</script><script id="pp_final_fsh" type="x-shader/x_fragment">#ifdef GL_ES
//precision mediump float;
precision highp float;
#endif
uniform sampler2D uSrc;
uniform sampler2D uBloom;
uniform vec2 uDelta;
varying vec2 texCoord;
varying vec2 screenCoord;
void main(void) {
	vec4 srccol = texture2D(uSrc, texCoord) * 2.0;
	vec4 bloomcol = texture2D(uBloom, texCoord);
	vec4 col;
	col = srccol + bloomcol * (vec4(1.0) + srccol);
	col *= smoothstep(1.0, 0.0, pow(length((texCoord - vec2(0.5)) * 2.0), 1.2) * 0.5);
	col = pow(col, vec4(0.45454545454545)); //(1.0 / 2.2)

	gl_FragColor = vec4(col.rgb, 1.0);
	gl_FragColor.a = 0.0;
}</script><script src="sakura.178f59c3.js"></script></main></div><script src="common.89489785.js"></script></body></html>